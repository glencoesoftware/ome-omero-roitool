plugins {
    id 'application'
    id 'eclipse'
}

group = 'com.glencoesoftware.roitool'
version = '0.2.0-SNAPSHOT'

mainClassName = 'com.glencoesoftware.roitool.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'http://artifacts.openmicroscopy.org/artifactory/maven/'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.jamonapi'
    exclude group: 'com.mortennobel'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'commons-codec'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'net.sf.ehcache'
    exclude group: 'org.apache.commons'
    exclude group: 'org.apache.httpcomponents'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
    exclude group: 'xalan'
}

dependencies {
    implementation 'org.openmicroscopy:omero-blitz:5.5.6'
    implementation 'com.zeroc:icegrid:3.6.4'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'info.picocli:picocli:3.9.3'
    testImplementation 'org.testng:testng:6.10'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Implementation-Version": version
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
                include 'LICENSE.txt'
            }
        }
    }
}

